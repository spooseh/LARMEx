{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "cc1fe71d-e74c-447d-a72c-743de694791b",
   "metadata": {},
   "source": [
    "```Julia\n",
    "using LinearAlgebra, Random, Distributions, DataFrames\n",
    "using MixedModels, CSV, CategoricalArrays, Printf\n",
    "\n",
    "\"\"\"\n",
    "    parLARMEx(; b=[], nAR=2, seed=0, nL2Max=100, nSamp=20000,\n",
    "                B_ar=.3, B_e=.3, b_var=[.03 .03 2])\n",
    "\n",
    "Construct the struct holding the settings for generating parameters for \n",
    "simulation.\n",
    "\n",
    "The default values of the keyword arguments specify a model with two \n",
    "temporally connected network nodes with one exogenous factor acting on both. \n",
    "## keyword arguments:\n",
    "- b = []: if provided with a matrix random-effects are extracted, \n",
    "            otherwise generated\n",
    "- nAR = 2: number of temporally connected nodes  \n",
    "- seed = 0: an integer to to replicate, 0 to initialize the \n",
    "              random-number-generatoranew\n",
    "- nL2Max = 100: how many random-effects to generate\n",
    "- nSamp = 20000: initial sample size for generating random-effects, \n",
    "                   see genRE_CS() for explanation\n",
    "- B_ar = .3: absolute value of fixed-effects autoregressive coefficients,\n",
    "               = [] for random values between 0.1 and 0.6\n",
    "- B_e = .3: absolute value of fixed-effects exogenous coefficients, \n",
    "              = [] for random values between 0.1 and 0.6\n",
    "- b_var = [.03 .03 .03]: for constructing a variance-covariance of RE \n",
    "\"\"\"\n",
    "mutable struct parLARMEx\n",
    "    nAR::Int      \n",
    "    seed::Int \n",
    "    rng ::AbstractRNG  \n",
    "    nL2Max::Int\n",
    "    nSamp::Int \n",
    "    \"Fixed-effects autoregressive coefficients\"\n",
    "    B_AR::Matrix{Float64} \n",
    "    \"Fixed-effects exogenous coefficients\"\n",
    "    B_E::Matrix{Float64}  \n",
    "    b_var::Matrix{Float64}\n",
    "    \"Variance-covariance of random-effects\"\n",
    "    b_cov::Matrix{Float64}\n",
    "    \"Random-effects autoregressive coefficients of size [nAR^2 x nL2Max]\"\n",
    "    b_ar::Matrix{Float64}\n",
    "    \"Random-effects exogenous coefficients of size [nAR x nL2Max]\"\n",
    "    b_e::Matrix{Float64}\n",
    "    \"Random-effects constant terms of size [nAR x nL2Max]\"\n",
    "    b_c::Matrix{Float64}\n",
    "    function parLARMEx(;b=[], nAR=2, seed=0, nL2Max=100, nSamp=20000,\n",
    "                        B_ar=.3, B_e=.3, b_var=[.03 .03 .03])\n",
    "        if seed === 0\n",
    "            rng = MersenneTwister(); \n",
    "        else\n",
    "            rng = MersenneTwister(seed);\n",
    "        end\n",
    "        nv = div(nAR,2)\n",
    "        if B_ar isa Number\n",
    "            B_AR = kron([1 -1 ; -1 1], B_ar .* ones(nv, nv))\n",
    "        elseif size(B_ar)[1] == nAR\n",
    "            B_AR = B_ar\n",
    "        else\n",
    "            B_AR =  kron([1 -1; -1 1], rand(rng, .1:.01:.6, nv, nv))\n",
    "        end\n",
    "        G = Diagonal(vcat(b_var[1]*ones(nAR^2), \n",
    "                     repeat(b_var[2:end], inner=nAR)));\n",
    "        if isempty(b)\n",
    "            b_ar, b_e, b_c, b_cov = genRE_CS(rng, G, B_AR, nL2Max, nSamp)\n",
    "        else\n",
    "            samp = sample(rng, 1:size(b)[1], nL2Max)\n",
    "            b_ar = b[samp, 1:nAR^2];\n",
    "            b_e  = b[samp, (nAR^2+1):(nAR^2+nAR)];\n",
    "            b_c  = b[samp, (nAR^2+nAR+1):(nAR^2+2*nAR)];\n",
    "            b_var = b_cov = reshape(Float64[], 0, 2)  \n",
    "            nSamp = 0\n",
    "        end\n",
    "        if isempty(B_e)\n",
    "            B_E =  kron([1; -1], rand(rng, .1:.01:.6, nv, 1)) \n",
    "        else\n",
    "            B_E = kron([1; -1], B_e .* ones(nv,1))\n",
    "        end\n",
    "        return new(nAR, seed, rng, nL2Max, nSamp, B_AR, B_E, b_var, \n",
    "                   b_cov, b_ar, b_e, b_c)\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    simLARMEx(; rng=MersenneTwister(), nObsL1=10, nL2=36, sigma=.02, \n",
    "                M0_max=.5, E=[])\n",
    "\n",
    "Construct the struct holding the settings for generating simulated data.\n",
    "\n",
    "By the default values of the keyword arguments it is assumed that the data \n",
    "generating process has a two-level structure. The multiple observations \n",
    "are made on level I and these are nested in level II units.\n",
    "## keyword arguments:\n",
    "- rng = MersenneTwister(): feed parLARMEx.rng for consistency and \n",
    "                           replication, initialized anew by default\n",
    "- nObsL1 = 10: number of observation on level I \n",
    "- nL2 = 36: number of units on level II\n",
    "- sigma = .02: variance of noise \n",
    "- M0_max = .5: determines the amplitude of initial values and exogenous \n",
    "               factors, 0.5 to keep trajectories mostly in [-1,1]\n",
    "- E = []: known exogenous factors of size [nL2 x nObsL1], \n",
    "            drawn randomly from [0,M0_max] by default\n",
    "\"\"\"\n",
    "mutable struct simLARMEx\n",
    "    nAR::Int      \n",
    "    nObsL1::Int       \n",
    "    nL2::Int       \n",
    "    sigma::Float64 \n",
    "    S0::Matrix{Float64}\n",
    "    E::Vector{Float64}\n",
    "    function simLARMEx(; rng=MersenneTwister(), nAR=2, nObsL1=10, nL2=36, \n",
    "                         sigma=.02, M0_max=.5, E=[]) \n",
    "        rangeS = range(0, stop=M0_max, length=50);\n",
    "        if E isa Number\n",
    "            E = E * ones(nL2*nObsL1)\n",
    "        elseif length(E) == (nL2*nObsL1)\n",
    "            E = E\n",
    "        else \n",
    "            E = sample(rng, rangeS, (nL2*nObsL1));\n",
    "        end\n",
    "        nv = div(nAR, 2)\n",
    "        S0 = sample(rng,[-1 1], nL2) .* hcat(sample(rng, rangeS, (nL2,nv)), \n",
    "                    sample(rng, -rangeS, (nL2, nv)))\n",
    "        return new(nAR, nObsL1, nL2, sigma, S0, E)\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    genRE_CS(rng, G, B_AR, nL2Max, nSamp)\n",
    "\n",
    "Generate random-effects parameter.\n",
    "\n",
    "For consistency it is advised that one instance of a random-number-generator \n",
    "should be used throughout the simulation. This function is called inside the \n",
    "constructor of parLARMEx and draws a sample of size nSamp from a multivariate \n",
    "normal Distributions MVN(0, G). Then using the fixed-effect autoregressive \n",
    "coefficients, B_AR, retains the random-effects for which the eigenvalues of \n",
    "the sum of fixed- and random-effects are less tha one. This guarantees that \n",
    "the autoregressive component of the process is stable. Finally a number of \n",
    "nL2Max parameter sets are returned in the matrices b_ar, b_e, b_c and their \n",
    "variance-covariance matrix as b_cov. \n",
    "## Example:\n",
    "b_ar, b_e, b_c, b_cov = genRE_CS(rng, G, B_AR, nL2Max, nSamp)\n",
    "\"\"\"\n",
    "function genRE_CS(rng, G, B_AR, nL2Max, nSamp) \n",
    "    nAR = size(B_AR)[1] # nE  = length(b_var)-1; d = nAR^2 + nAR*nE;\n",
    "    mvn = MvNormal(G);\n",
    "    ri  = rand(rng, mvn, nSamp);\n",
    "    ind = all.(eachslice(abs.(ri[1:(nAR^2+1*nAR), :]) .< .9, dims=2));\n",
    "    ri  = ri[:,ind];\n",
    "    r1  = reshape(ri[1:nAR^2, :], nAR, nAR, size(ri)[2]) .+ B_AR;\n",
    "    eg  = eigvals.(eachslice(r1, dims=3));\n",
    "    ind = [!any(abs.(i).>1) for i in eg];\n",
    "    ri  = ri[:, ind]; \n",
    "\n",
    "    a = 1:nAR^2;\n",
    "    e = (nAR^2+1):(nAR^2+nAR);\n",
    "    c = (nAR^2+nAR+1):(nAR^2+2*nAR);\n",
    "    \n",
    "    samp = sample(rng, axes(ri, 2), nL2Max)\n",
    "    b_ar = ri[a, samp]'\n",
    "    b_e  = ri[e, samp]'\n",
    "    b_c  = ri[c, samp]'\n",
    "    return b_ar, b_e, b_c, cov(ri');\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    genData(P, S)\n",
    "\n",
    "Generate simulated data given two struct of parameter and data specifications.\n",
    "\n",
    "It returns the simulated data along with the noiseless data as dataframes and \n",
    "the signal-to-noise-ratio SNR.\n",
    "## Example:\n",
    "dfData, dfData0, SNR = genData(parLARMEx(), simLARMEx()); \n",
    "\"\"\"\n",
    "function genData(P, S)\n",
    "    data1 = zeros(S.nL2*S.nObsL1, P.nAR)\n",
    "    data0 = copy(data1)\n",
    "    rho = 0 # Noise_rho;\n",
    "    Sigma =  Matrix((S.sigma-rho)I, P.nAR, P.nAR) .+ rho\n",
    "    for i in 1:S.nL2 \n",
    "        BAR = P.B_AR + reshape(P.b_ar[i,:], P.nAR, P.nAR)'; # columnwise\n",
    "        E1   = S.E[((i-1)*S.nObsL1+1):(i*S.nObsL1)]\n",
    "        mvn = MvNormal(Sigma);\n",
    "        d1 = copy(transpose(rand(P.rng, mvn, S.nObsL1)))\n",
    "        d0 = 0 .* d1;\n",
    "        d1[1,:] = S.S0[i,:]\n",
    "        d0[1,:] = S.S0[i,:]\n",
    "        ib = 1\n",
    "        for t in 2:(S.nObsL1)\n",
    "            d1[t,:] += BAR * d1[t-1,:] + (P.B_E .+ P.b_e[i,:]) * E1[t]\n",
    "                        + P.b_c[i,:]\n",
    "            d0[t,:] += BAR * d0[t-1,:] + (P.B_E .+ P.b_e[i,:]) * E1[t]\n",
    "                        + P.b_c[i,:]\n",
    "        end\n",
    "        data1[((i-1)*S.nObsL1+1):(i*S.nObsL1),:] = d1[1:S.nObsL1,:]\n",
    "        data0[((i-1)*S.nObsL1+1):(i*S.nObsL1),:] = d0[1:S.nObsL1,:]\n",
    "    end\n",
    "\n",
    "    tL1 = repeat(1:S.nObsL1, S.nL2);\n",
    "    idL2 = repeat(1:S.nL2, inner=S.nObsL1);\n",
    "    colM = map(string, repeat([\"M\"], P.nAR), 1:P.nAR)\n",
    "    if all(S.E .== 0)\n",
    "        dfData  = DataFrame(idL2=idL2, tL1=tL1);\n",
    "        dfData0 = DataFrame(idL2=idL2, tL1=tL1);\n",
    "        cols = colM\n",
    "    else\n",
    "        dfData  = DataFrame(idL2=idL2,tL1=tL1,E=S.E);\n",
    "        dfData0 = DataFrame(idL2=idL2,tL1=tL1,E=S.E);\n",
    "        cols = vcat(colM, [\"E\"])\n",
    "    end\n",
    "    for i in 1:P.nAR\n",
    "        insertcols!(dfData ,i+2,cols[i] => data1[:,i]);\n",
    "        insertcols!(dfData0,i+2,cols[i] => data0[:,i]);\n",
    "    end\n",
    "    SNR = var(data0) / Sigma[1,1]\n",
    "    return dfData,dfData0,SNR; \n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    prepData2Fit(rawData, idL2, arList, exList)\n",
    "\n",
    "Prepare simulated\\real data to be fit by LARMEx.\n",
    "\n",
    "It is assumed that data has been acquired by ecological momentary \n",
    "assessment where respondent are observed multiple times in the course \n",
    "of several days or weeks.\n",
    "## Arguments:\n",
    "- rawData: simulated or real data as a dataframe\n",
    "- idL2: column name for level II units, e.g., an id for each day,\n",
    "        \"idL2\" here\n",
    "- arList: list of temporelly connected symptoms, [\"M1\",\"M2\"] here\n",
    "- exList: list of exogenous factors together with the constant terms, \n",
    "          [\"E\",\"C\"] here \n",
    "\"\"\"\n",
    "function prepData2Fit(rawData, idL2, arList, exList)\n",
    "    nAR = length(arList)\n",
    "    nEX = length(exList)\n",
    "    colM = map(string, repeat(arList, inner=nAR), repeat(1:nAR, nAR));\n",
    "    col  = [\"idL2\"; \"tL1\"; \"M\"; colM]\n",
    "    if \"E\" in exList\n",
    "        col = vcat(col, map(string, repeat([\"E\"], nAR), 1:nAR))\n",
    "    end\n",
    "    if \"C\" in exList\n",
    "        col = vcat(col, map(string, repeat([\"C\"], nAR), 1:nAR))\n",
    "    end\n",
    "    D = reshape(Float64[], 0, 3+nAR^2+nAR*nEX)\n",
    "    l2ID = unique(rawData.idL2);\n",
    "    for sj in l2ID\n",
    "        d1 = rawData[rawData.idL2.==sj, :];\n",
    "        nObs = size(d1,1);\n",
    "        iD = repeat(Matrix(d1[2:end, 1:2]), nAR);\n",
    "        dS = Matrix(d1[2:nObs, arList]);\n",
    "        S = reshape(dS, nAR*(nObs-1), 1);\n",
    "        dL = kron(Matrix(1I, nAR, nAR), Matrix(d1[1:(nObs-1), arList]));\n",
    "        cat = hcat(iD, S, dL)\n",
    "        if \"E\" in exList\n",
    "            dE = kron(Matrix(1.0I, nAR, nAR), Matrix(d1[2:nObs, [\"E\"]]));\n",
    "            cat = hcat(cat, dE)\n",
    "        end\n",
    "        if \"C\" in exList\n",
    "            C = repeat([1], nObs-1);\n",
    "            dC = kron(Matrix(1I, nAR, nAR), C);\n",
    "            cat = hcat(cat, dC);\n",
    "        end\n",
    "        D = vcat(D, cat)\n",
    "    end\n",
    "    fitData = DataFrame(D, col);\n",
    "    fitData[!, 1:2] = convert.(Int16, fitData[:, 1:2]);\n",
    "    fitData[!, idL2] = categorical(fitData[:, idL2]);\n",
    "    return fitData; \n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    setFormula(fitData)\n",
    "\n",
    "Generate a mixed-effects formula from a specifically formatted dataframe.\n",
    "\n",
    "The input is supposed to have a special format where in a two-level \n",
    "longitudinal data the level II identifiers are in the first and the \n",
    "stacked observation in the third columns. The columns 4:end are \n",
    "supposed to represent the network structure and constant terms.\n",
    "## Example:\n",
    "names(fitData):\n",
    "11-element Vector{String}: [\"idL2\",\"tL1\",\"M\",\"M11\",\"M12\",\"M21\",\"M22\",\n",
    "                            \"E1\",\"E2\",\"C1\",\"C2\"]\n",
    "setFormula(fitData): \"M ~ 0 + M11 + ...\n",
    "                          + (0 + M11 + ... | idl2)\"                         \n",
    "\"\"\"\n",
    "function setFormula(fitData) \n",
    "    cols = names(fitData);\n",
    "    idL2 = cols[1];\n",
    "    colM  = cols[3];\n",
    "    colRE = cols[4:end];\n",
    "    indC = findall(x -> occursin(\"C\", x), colRE);\n",
    "    colFE = copy(colRE);\n",
    "    deleteat!(colFE, indC);\n",
    "    frm = string(colM, \" ~ 0 +\", join(colFE, '+'), \n",
    "                 \" + (0 +\", join(colRE, '+'), \"|\", idL2, \")\")\n",
    "    return @eval(@formula($(Meta.parse(frm)))); \n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    re2csv(P, n, arList, exList, fName)\n",
    "\n",
    "Save the random-effects from a parLARMEx instance as a CSV file.\n",
    "\"\"\"\n",
    "function re2csv(P, n, arList, exList, fName)\n",
    "    nAR = P.nAR\n",
    "    col = map(string, repeat(arList, inner=nAR), repeat(1:nAR, nAR));\n",
    "    D = P.b_ar[1:n, :]\n",
    "    if \"E\" in exList\n",
    "        col = vcat(col, map(string, repeat([\"E\"], nAR), 1:nAR))\n",
    "        D = hcat(D, P.b_e[1:n, :])\n",
    "    end\n",
    "    if \"C\" in exList\n",
    "        col = vcat(col, map(string, repeat([\"C\"], nAR), 1:nAR))\n",
    "        D = hcat(D, P.b_c[1:n, :])\n",
    "    end\n",
    "    re = DataFrame(D, col)\n",
    "    CSV.write(fName, re)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    fe2csv(P, arList, exList, fName)\n",
    "\n",
    "Save the fixed-effects from a parLARMEx instance as a CSV file.\n",
    "\"\"\"\n",
    "function fe2csv(P, arList, exList, fName)\n",
    "    nAR = P.nAR\n",
    "    col = map(string, repeat(arList, inner=nAR), repeat(1:nAR, nAR));\n",
    "    D = reshape(P.B_AR', (1, nAR^2))\n",
    "    if \"E\" in exList\n",
    "        col = vcat(col, map(string, repeat([\"E\"], nAR), 1:nAR))\n",
    "        D = hcat(D, reshape(P.B_E, (1, nAR)))\n",
    "    end\n",
    "    if \"C\" in exList\n",
    "        col = vcat(col, map(string, repeat([\"C\"], nAR), 1:nAR))\n",
    "        D = hcat(D, zeros(1,nAR))\n",
    "    end\n",
    "    if length(D) == length(P.b_var) # full b_var is provided\n",
    "        D = vcat(D, reshape(P.b_var, (1, nAR^2+2*nAR)))\n",
    "    else\n",
    "        bv = hcat(P.b_var[1]*ones(nAR^2), repeat(P.b_var[2:end], inner=nAR))\n",
    "        D = vcat(D, reshape(bv, (1, nAR^2+2*nAR)))\n",
    "    end\n",
    "    fe = DataFrame(D, col)\n",
    "    CSV.write(fName, fe)\n",
    "end\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
